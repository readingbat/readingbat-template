plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "com.github.ben-manes.versions" version "0.29.0"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

repositories {
    maven { url 'https://maven-central.storage-download.googleapis.com/repos/central/data/' }
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url 'https://kotlin.bintray.com/kotlin-js-wrappers' }
    jcenter()
    maven { url 'https://jitpack.io' }
}

def mainName = 'ContentServer'
def appName = 'server'

// This is for ./gradlew run
application {
    mainClassName = mainName
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description 'ReadingBat Site'
group 'com.github.readingbat'
version '1.0.0'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.readingbat:readingbat-core:$readingbat_version"
    implementation "com.github.pambrose.common-utils:core-utils:$utils_version"
    implementation "io.github.microutils:kotlin-logging:$logging_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"

    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
}

// Include build uberjars in heroku deploy
task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    archiveFileName = 'server.jar'
    manifest {
        attributes('Implementation-Title': appName)
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}